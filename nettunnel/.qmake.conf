# 生成程序路径
DESTDIRBASE = $$PWD/bin

BINHOME = $$PWD/bin
unix{
    linux {
        DESTDIRBASE = $${BINHOME}/unix/linux
    }
    android {
        DESTDIRBASE = $${BINHOME}/unix/android
    }
}
win32{
    DESTDIRBASE = $${BINHOME}/win
    win32-msvc*{
        DESTDIRBASE = $${BINHOME}/win/win-msvc
    }
    #mingw
    win32-g++{
        DESTDIRBASE = $${BINHOME}/win/win-mingw
    }
}

INCLUDEPATH += $$PWD/public/
#PRECOMPILED_HEADER = $$PWD/public/stdafxView.h  #一部分头文件加入编译预处理，提高编译速度

# 这里生成文件，存放定义的一些宏定义
baseMacroDefinefile = $$PWD/public/baseMacroDefine.h
!build_pass {
    TESTSTRING = $$PWD/sss
    headerContents = \
        #"$${LITERAL_HASH}pragma once" \
        "$${LITERAL_HASH}ifndef BASEMACRODEFINE_H" \
        "$${LITERAL_HASH}define BASEMACRODEFINE_H" \
        "  " \
        "$${LITERAL_HASH}define TESTSTRING \"$${TESTSTRING}\"" \
        "  " \
        "$${LITERAL_HASH}endif //BASEMACRODEFINE_H" \

    write_file($${baseMacroDefinefile}, headerContents)
}

win32{
    #程序版本
    VERSION = 1.0.0
    #程序图标
    #RC_ICONS = icon.ico
    #公司名称
    QMAKE_TARGET_COMPANY = "mycompany"
    #程序说明
    QMAKE_TARGET_DESCRIPTION = "mysoftware"
    #版权信息
    QMAKE_TARGET_COPYRIGHT = "Copyright(C) 2025"
    #产品名称
    QMAKE_TARGET_PRODUCT = "app"
    #程序语言
    #0x0800代表和系统当前语言一致
    RC_LANG = 0x0800
}

#win32 {
#    SRCUIDIR = $$PWD/public/baseMacroDefine.h
#    DESTUIDIR = $$PWD
#    QMAKE_POST_LINK = xcopy /E/Y/H/C/I $$replace(SRCUIDIR,/,\\) $$replace(DESTUIDIR,/,\\)
#}
